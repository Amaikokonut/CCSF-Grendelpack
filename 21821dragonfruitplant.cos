**
** dragonfruit plant script
**
** Sprites by Jesseth, coding by Pilla.
**
**
** Feel free to use this code however you want.
**
**

**
** Caos2pray chunk
**
** Compiles the agent.
**
* CAOS2PRAY CHUNK
*# Pray-File "gp_dragonfruit_plant_JESS.agents"
*# DS-Name "dragonfruit Plant (Jesseth)"
*# attach gp_dragonfruit_plant_JESS.c16 
*# desc = "Some pretty dragonfruit."
*# Agent Animation File = "gp_dragonfruit_plant_JESS.c16"
*# Agent Sprite First Image = 20
*# Agent Animation Gallery = "gp_dragonfruit_plant_JESS"
*
*

*
** Injection script
**

**
** The dragonfruit mother
**

*new: comp 2 23 21821 "gp_dragonfruit_plant_JESS" 1 0 rand 800 900
*mesg wrt+ targ 1000 game "CreatorX" game "CreatorY" 0

**
** SCRIPTS
**
**
**

**
** Creator script
**

scrp 2 23 21821 1000
	new: comp 2 23 21821 "gp_dragonfruit_plant_JESS" 1 0 rand 800 900
** Attributes: mouseable 2 + activateable 4 + collide 64 + physics 128 = 198
	attr 198
	perm 100
	accg 4
	aero 1
	fric 80
	elas 20
** bhvr: act1 + hit 8 = 9
	bhvr 9

** Create the spawning spots
	pat: dull 2 "gp_dragonfruit_plant_JESS" 14 6 44 2
	pat: dull 3 "gp_dragonfruit_plant_JESS" 14 23 78 2
	pat: dull 4 "gp_dragonfruit_plant_JESS" 14 64 35 2
	pat: dull 5 "gp_dragonfruit_plant_JESS" 14 79 63 2

** Create the lights
	pat: dull 6 "gp_dragonfruit_plant_JESS" 1 0 0 1

** Create the dragonfruit dispenser animation
	pat: dull 7 "gp_dragonfruit_plant_JESS" 5 49 132 1

** Create the dragonfruit dispenser button
	pat: butt 8 "gp_dragonfruit_plant_JESS" 3 2 49 132 2 [] 0 1

	tick 20

	mvsf _p1_ _p2_
	targ ownr
	kill ownr
endm


**
** POPULATION MONITOR
**

 
scrp 1 1 21821 9
	tick rand 1500 8000
	setv va00 0
	esee 2 4 21821
		addv va00 1
	next
	doif va00 le 1
		setv va02 posx
		setv va03 posy
		reps 3
			inst
			new: simp 2 8 21821 "gp_dragonfruit_plant_JESS" 2 17 rand 1800 1900
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr eat 16 + pickup 32 = 48
			attr 194
			bhvr 48
			accg 1
			fric 25
			elas 45
			velo rand -8 8 0
			tick rand 1 500
			mvsf va02 va03
			slow
		repe
	endi
endm


**
** DRAGONFRUIT MOTHER
**

*
** HIT SCRIPT

scrp 2 23 21801 3
	stim writ from 92 1
	setv va05 posx
	setv va06 posy
	subv va06 20
** Create fruit
	new: simp 2 8 21821 "gp_dragonfruit_plant_JESS" 2 17 rand 1800 1900
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr eat 16 + pickup 32 = 48
	attr 194
	bhvr 48
** Will go through some walls
	perm 80
** Physics
	accg 0.6
	aero 1
	fric 25
	elas 45
	tick rand 800 1600
	mvsf va05 va06
	velo rand -4 4 rand -4 -1
	slow

endm


*
** PUSH SCRIPT

scrp 2 23 21821 1

	doif from eq pntr
		mesg writ pntr 101
	else
		stim writ from 90 1
	endi


	setv va05 posl
	setv va06 post

	lock

** animate the button
	part 8
	frat 10
	anim [1 1 1 0]

** animate the vendor opening
	part 7
	frat 8
	anim [1 2 3 4 ]
	over

** create the macaroon
	addv va05 55
	addv va06 159

	inst
	pose 3
	new: simp 2 11 21821 "gp_dragonfruit_plant_JESS" 1 19 rand 2000 2200
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr eat 16 + pickup 32 = 48
	attr 194
	bhvr 48
** won't go through walls
	perm 100
** Physics
	accg 0.6
	aero 1
	fric 25
	elas 40
** emit fat
	emit 8 0.6
** tick before disappearing
	tick rand 1200 1800
	mvto va05 va06

	targ ownr
	part 7
	anim [ 3 2 1 0]
	over
	unlk

endm


*
** TIMER SCRIPT

scrp 2 23 21821 9
	doif movs eq 0
** Reset the tick
		tick 0

** animate the lamps
		setv va50 rand 0 1
		setv va51 rand 0 1
		setv va52 rand 0 1
		setv va53 rand 0 1
		setv va54 rand 0 1
		setv va55 rand 0 1
		sets va56 "0 "
		adds va56 vtos va50
		adds va56 " 0 0 0 0 0"
		adds va56 vtos va51
		adds va56 " 0 0 0 0 0"
		adds va56 vtos va52
		adds va56 " "
		adds va56 vtos va53
		adds va56 " "
		adds va56 vtos va54
		adds va56 " 0 0 0 0 0"
		adds va56 vtos va55
		adds va56 " 0 255"
		part 6
		frat 6
		anms va56
** Grow the flowers and spawn the fruit
		setv va01 2
		reps 4
			part va01
** pose ge 3 --> fully grown, drop fruit now
			doif pose ge 3
				inst
** reset flower bud
				pose 0
** Create and drop a new dragonfruit
				setv va05 posl
				setv va06 post

				doif va01 eq 2
					addv va05 6
					addv va06 44
				elif va01 eq 3
					addv va05 23
					addv va06 78
				elif va01 eq 4
					addv va05 64
					addv va06 35
				else
					addv va05 79
					addv va06 63
				endi

** Create fruit
				new: simp 2 8 21821 "gp_dragonfruit_plant_JESS" 2 17 rand 1800 1900
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr eat 16 + pickup 32 = 48
				attr 194
				bhvr 48
** Will go through some walls
				perm 80
** Physics
				accg 0.6
				aero 1
				fric 25
				elas 45
				tick rand 800 1600
				mvsf va05 va06
				slow

** pose ge 1 --> grow 
			elif pose ge 1
				setv va07 pose
				addv va07 1
				pose va07
			endi
			addv va01 1
			targ ownr
		repe

** Check how many fruitsies there are currently growing
		setv va00 0
		setv va01 2
		reps 4
			part va01
			doif pose ne 0
				addv va00 1
			endi
			addv va01 1
		repe

** If there are less than 3 seed lobes growing, there may grow another one.
		doif va00 lt 3
			setv va03 0
** Make the first checking point random.
			setv va01 2
			addv va01 rand 0 3

** If the first spot isn't free, try the next one.
			reps 4
				part va01
				doif pose eq 0 and va03 eq 0
					setv va03 1
					pose va03
				endi
				addv va01 1
				doif va01 gt 5
					setv va01 2
				endi
			repe
		endi

		targ ownr
		tick rand 700 1200
	endi
endm

*
** MACAROON
* 

** macaroon eat script
scrp 2 11 21821 12
	lock
	stim writ from 79 1
	wait 25
	kill ownr
	unlk
endm

** macaroon timer script
scrp 2 11 21821 9
	doif carr eq null
		kill ownr
	endi
endm


** FRUIT
* 

** fruit eat script
scrp 2 8 21821 12
	lock
	stim writ from 78 1
	wait 25
	doif pose eq 0
		pose 1
	else
		kill ownr
	endi
	unlk
endm

** fruit timer script
scrp 2 8 21821 9

** Get the room type of the seed
	setv va00 posl
	subv va00 14
	setv va01 posb
	subv va01 40
	setv va02 grap posx posy
	setv va03 rtyp va02


** Check if the seed has settled
	doif carr eq null and fall eq 0
		doif va03 ne 5 and va03 ne 6 and va03 ne 7
			kill ownr
			stop
		else

			inst
** Grow plant!
			new: comp 2 4 21821 "gp_dragonfruit_plant_JESS" 4 10 rand 1000 3000
** attr: invisible 16, bhvr: 0
			attr 16
			bhvr 0
			pat: dull 2 "gp_dragonfruit_plant_JESS" 14 11 0 1

** Set the tick to 2 to trigger the 'do I have enough space to grow' check script
			tick 2

** test-move it into the space: if it can't grow there, kill it
			doif tmvt va00 va01 eq 0
				kill targ
			else
** If it can grow here, grow!
				mvto va00 va01
			endi
			slow
			targ ownr
			kill ownr
		endi
	endi
endm


** PLANT
*

** plant growing timer

scrp 2 4 21821 9
** Check if the plant is about to start growing: if yes, check if there aren't too many other plants around.
	doif tick eq 2
** Check if there's enough space to grow: if not, kill the plant placeholder
		inst
		setv va00 0
		rnge 40
		esee 2 4 21821
			addv va00 1
		next

		doif va00 ge 1
			kill ownr
			stop
		endi

		setv va00 0
		rnge 210
		esee 2 4 21821
			addv va00 1
		next
		doif va00 ge 2
			kill ownr
			stop
		endi
** Set the plant growing timer
		tick rand 160 300
		slow
	endi
** Check if the plant is fully grown. If not, grooww
	doif pose lt 3
		setv va00 pose
		addv va00 1
		pose va00
** If the plant is fully grown
	elif pose eq 3
** check how many times a fruit has been dropped: if more than 2 times kill the plant.
		doif ov00 gt 2
			targ ownr
** Kill it ;u;
			lock
			part 2
			pose 0

			targ ownr
			part 0
			frat 40
			anim [2 1 0]
			over
			kill ownr
			stop
			unlk

** If less than/equal to 2 times, do the grow subroutines

		else

** Make/grow fruit
			part 2
** fruit part pose
			doif pose ge 8
				inst
				pose 0

				setv va05 posl
				setv va06 post
				addv va05 11
				addv va06 0
				addv ov00 1

				new: simp 2 8 21821 "gp_dragonfruit_plant_JESS" 2 17 rand 1800 1900
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr eat 16 + pickup 32 = 48
				attr 194
				bhvr 48
** Will go through some walls
				perm 80
** Physics
				accg 0.6
				aero 1
				fric 25
				elas 45
				velo rand -2 2 0
				tick rand 800 1600
				mvsf va05 va06
				slow


** pose lt 3 -> grow
			else
				setv va07 pose
				doif pose eq 0
					addv va07 5
				endi

				addv va07 1
				pose va07
			endi
		endi
	endi

endm


*
** Removal script 
*

rscr

enum 2 23 21821
	kill targ
next
enum 2 4 21821
	kill targ
next
enum 2 8 21821
	kill targ
next
enum 2 11 21821
	kill targ
next

scrx 2 23 21821 9
scrx 2 23 21821 3
scrx 2 23 21821 1
scrx 2 23 21821 1000

scrx 2 4 21821 9
scrx 2 8 21821 9
scrx 2 8 21821 12
scrx 2 11 21821 9
scrx 2 11 21821 12