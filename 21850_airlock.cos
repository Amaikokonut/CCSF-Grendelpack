* Dummy CA emitter for the esee function
new: simp 1 1 21836 "blnk" 1 0 10
rnge 1800
setv ov50 1
seta va99 targ
mvto 24580 160

* The airlock buttons

new: comp 1 1 21850 "gp_airlock" 58 10 100
pat: butt 2 "gp_airlock" 0 2 2 157 1 [] 1001 0
pat: butt 3 "gp_airlock" 2 2 359 146 1 [] 1001 0
pat: dull 4 "gp_airlock" 4 354 118 1
pat: dull 5 "gp_airlock" 10 44 71 1
pat: dull 6 "gp_airlock" 30 428 15 1
part 4
frat 10

* Get the 3 rooms: 00 airlock, 01 meso, 02 space
setv ov00 grap 24419 174
setv ov01 grap 24218 181
setv ov02 grap 24678 181
seta ov03 va99
door ov00 ov02 0

mvto 24190 54


* The airlock magic script!
scrp 1 1 21836 1010
	doif ov50 eq 1
		inst
		esee 0 0 0
			setv va00 posx
			setv va01 posy
			doif grap va00 va01 eq grap 24419 174
				doif targ <> ownr and carr = null
					doif fmly <> 1
						doif fmly = 2 and gnus = 1
						else
							doif attr lt 64
								kill targ
							else
								subv va01 10
								velo rand 15 20 rand -10 -15
								mvsf va00 va01
								attr 198
								elas 80
								accg 1
								aero 1
								fric 1
							endi
						endi
					endi
				endi
			endi
		next

		wait 10
		inst
		esee 0 0 0
			doif grap posx posy eq grap 24419 174 or grap posx posy eq grap 24787 174
				doif targ <> ownr and carr = null
					doif fmly <> 1
						doif fmly = 2 and gnus = 1
						else
							setv va00 posx
							setv va01 posy
							subv va00 41
							subv va01 39
							doif fmly = 4
								dead
								setv va66 1
							else
								setv va66 0
							endi
							kill targ

							reps 4
								new: simp 1 1 46 "dust cloud" 4 8 1000
								doif tmvt va00 va01 <> 1
									kill targ
								else
									mvto va00 va01
									setv vely rand -5 5
									setv velx rand -5 5
									attr 192
									accg 0.1
									elas 100
									anim [0 1 2 3]
									tick rand 30 40
								endi
							repe
							doif va66 = 1
								reps 8
									new: simp 1 1 46 "bone" 12 0 1000
									doif tmvt va00 va01 <> 1
										kill targ
										stop
									endi
									mvto va00 va01
									setv vely rand -5 5
									setv velx rand -5 5
									attr 192
									accg 0
									elas 100
									anim [0 1 2 3 4 5 6 7 8 9 10 11 255]
									tick rand 50 60
								repe
							endi
						endi
					endi
				endi
			endi
		next


	endi
endm


scrp 1 1 21850 1001
	lock

* Change button poses to disabled
	part 2
	pose 1
	part 3
	pose 1
* Close the door from the airlock to the meso
	door ov00 ov01 0

* Subroutine close entrance door and animation countdown
	gsub ctdown

* Subroutine animation open door to spaaace
	gsub spopen

* Open door to space
	door ov00 ov02 100

* Do the airlock magic
	seta va01 ov03
	mesg writ va01 1010



	wait 40

* Close door to space
	door ov00 ov02 0

*Subroutine animation close door and unlock chamber
	gsub unlock

* Open door to meso
	door ov00 ov01 100

* Change button poses to enabled
	part 2
	pose 0
	part 3
	pose 0


	subr ctdown
		targ ownr
		part 5
		anim [1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19]
		over
		part 4
		anim [1 2 3 4 5]
		over
	retn

	subr spopen
		targ ownr
		part 6
		anim [1 2 3 4 5 7 11 15 19 23 26]
		over
	retn

	subr unlock
		targ ownr
		part 6
		anim [24 23 22 21 20 19 18 17 16 15 14 13 12 11 10 9 8 7 6 5 4 3 2 1 0]
		part 4
		anim [4 3 2 1 0]
		over
		part 5
		anim [18 17 16 15 14 13 12 11 10 9 8 7 6 5 4 3 2 1 0]
		over
	retn


endm
rscr
enum 1 1 21850
	kill targ
next
enum 1 1 21836
	doif ov50 eq 1
		kill targ
	endi
next