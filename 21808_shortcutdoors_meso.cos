**
** TELEPORTERS
**

doif game "gpmain" ne 0 and game "gpmeso" eq 0
*
** DESERT (meso) > DESERT (main)
	new: comp 2 2 21808 "gp_shortcutdoors_CS" 3 2 100
	pat: butt 2 "gp_shortcutdoors_CS" 0 2 18 57 0 [] 1004 0
	setv ov00 1
	setv ov01 0
	attr 4
	bhvr 1
	mvto 23695 911
	setv va00 grap posx posy
*
** DESERT (main) > DESERT (meso)
	new: comp 2 2 21808 "gp_shortcutdoors_CS" 3 2 100
	pat: butt 2 "gp_shortcutdoors_CS" 0 2 18 57 0 [] 1004 0
	setv ov00 1
	setv ov01 0
	attr 4
	bhvr 1
	mvto 22010 1091
	link grap posx posy va00 60
*
** JUNGLE (meso) > SWAMP (main)
	new: comp 2 2 21808 "gp_shortcutdoors_CS" 3 5 100
	pat: butt 2 "gp_shortcutdoors_CS" 0 2 18 57 0 [] 1004 0
	setv ov00 2
	attr 4
	bhvr 1
	mvto 25920 1567
	setv va00 grap posx posy
*
** SWAMP (main) > JUNGLE (meso)
	new: comp 2 2 21808 "gp_shortcutdoors_CS" 3 5 100
	pat: butt 2 "gp_shortcutdoors_CS" 0 2 18 57 0 [] 1004 0
	setv ov00 2
	attr 4
	bhvr 1
	mvto 21927 2299
	link grap posx posy va00 60
endi

doif game "gpmeso" ne 1
*
** UPPERWATER (meso) > UNDERWATER (meso)
	new: comp 2 2 21808 "gp_shortcutdoors_CS" 3 8 100
	pat: butt 2 "gp_shortcutdoors_CS" 0 2 18 57 0 [] 1004 0
	setv ov00 3
	attr 4
	bhvr 1
	mvto 23619 1492
	setv va00 grap posx posy
*
** UNDERWATER (meso) > UPPERWATER (meso) 
	new: comp 2 2 21808 "gp_shortcutdoors_CS" 3 8 100
	pat: butt 2 "gp_shortcutdoors_CS" 0 2 18 57 0 [] 1004 0
	setv ov00 3
	attr 4
	bhvr 1
	mvto 23619 1920
	link grap posx posy va00 60
endi

**
** TELEPORTER EVENT SCRIPTS
**
**

*
** Disable
scrp 2 2 21808 1004
	doif ov01 eq 0
		setv va00 ov00
		enum 2 2 21808
			doif ov00 eq va00
				attr 16
				setv ov01 1
				part 2
				pose 1
			endi
		next
	else
		setv va00 ov00
		enum 2 2 21808
			doif ov00 eq va00
				attr 4
				setv ov01 0
				part 2
				pose 0
			endi
		next
	endi
endm
*
** PUSH
scrp 2 2 21808 1
	doif ov01 eq 0
		seta va52 from
		setv va00 ov00
		seta va50 targ
		seta va51 targ
		enum 2 2 21808
			doif ov00 eq va00
				anim [1 2 1 0]
				doif targ ne va50
					seta va51 targ
				endi
			endi
		next
		targ va51
		setv va00 posx
		setv va01 posy
		targ va52
		doif fmly eq 4
			lock
			doif tmvf va00 va01 eq 1
				targ va50
				attr 16
				anim [0 0 0 0 0 0 0 0]
				over
				attr 4
				targ va51
				attr 16
				targ va52
** travelled through door
				stim writ from 95 1
** no more attention on door
				urge writ targ 2 -2 1 -2
				mvft va00 va01
** Travelled through door
** Discourage from pushing door any further
				targ va51
				anim [0 0 0 0 0 0 0 0]
				over
				attr 4
			endi
			unlk
		endi
		doif va52 eq pntr
			cmrp va00 va01 1
		endi
	endi
endm



rscr
enum 2 2 21808
	kill targ
next