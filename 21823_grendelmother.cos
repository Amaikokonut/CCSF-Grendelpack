**
** Caos2pray chunk
**
** Compiles the agent.
**
* CAOS2PRAY CHUNK
*# Pray-File "gp_grendel_mother_PI.agents"
*# DS-Name "Grendel Mother thing (Pilla)"
*# attach gp_grendelmother_PILLA.c16 
*# depend blnk.c16
*# desc = "Grendel egg layer."
*# Agent Animation File = "gp_grendelmother_PILLA.c16"
*# Agent Sprite First Image = 0
*# Agent Animation Gallery = "gp_grendelmother_PILLA"
*
*

**
** Grendel egg maker script.
** This goes through the grendel egg agents you have installed.
** Functions: left/right buttons, male/female switch, breed name, go button
**

** new: comp 3 3 21823 "gp_grendelmother_PILLA" 1 0 100


**grendel egg maker
scrp 3 3 21823 1000
	attr 18
** text box, GO button, left button, right button, gender button
	pat: fixd 1 "gp_grendelmother_PILLA" 1 22 19 0 "whiteontransparentchars"
	pat: butt 2 "gp_grendelmother_PILLA" 1 1 22 19 1 [] 1001 0
	pat: butt 3 "gp_grendelmother_PILLA" 2 1 0 19 1 [] 1001 0
	pat: butt 4 "gp_grendelmother_PILLA" 3 1 148 19 1 [] 1001 0
	pat: butt 5 "gp_grendelmother_PILLA" 4 2 22 0 1 [] 1001 0

** Females default. Because I can.
	setv ov00 2
	mvto _p1_ _p2_
	tick 1
endm

** The initialize script: search for a grendel breed in your agents folder to show initially.
scrp 3 3 21823 9
* va99: grendel genetics found
* va98: more than 100 breeds searched
	setv va99 0
	setv va98 0
* va97: last breed displayed
	sets va97 ""

	inst
* get the next grendel egg agent
	loop
		sets ov90 pray next "EGGS" va97
		sets va97 ov90
		sets va01 pray agts ov90 "Genetics File" ""
		sets va02 subs va01 1 4
		doif va02 eq "gren"
			setv va99 1
		endi
		addv va98 1
	untl va99 eq 1 or va98 gt 100
* if more than 100 agents searched, there are no grendel egg agents. This shouldn't happen since we include one.
	doif va98 gt 100
		sets ov90 ""
		targ ownr
		part 1
		ptxt "Nothing found"
	else
* Display the grendel breed name!
		gsub display_name
	endi
	slow
* Disable ticks
	tick 0

** Subroutine to display name in name bar
	subr display_name
		sets va01 ov90
		targ ownr
		part 1
		ptxt va01
	retn
endm

** Button click script
scrp 3 3 21823 1001
* refresh the pray agents file folder
	pray refr
* GO button
	doif _p1_ eq 2
* If there is a grendel genome loaded, pick that genome and lay an egg
		doif ov90 ne ""
			gsub lay_egg
		else
* If not, do nothing and display nothing found text on the info bar.
			targ ownr
			part 1
			ptxt "Nothing found"
		endi

* LEFT button
	elif _p1_ eq 3
		gsub prev_genome

* RIGHT button
	elif _p1_ eq 4
		gsub next_genome

* GENDER button
	elif _p1_ eq 5
		gsub set_gender
	endi
*	
** Subroutines
*

** Lay the egg	
	subr lay_egg

		inst

* Check some... stuff.
		setv va00 pray test ov90
		setv va23 pray deps ov90 1

		sets va99 ov90
		rtar 1 1 161
		doif targ ne null
			lock
			wait 1
		endi
		targ ownr

* If va23 != 0 then we have problems
		doif va23 ne 0 or va00 eq 0
			targ ownr
			part 1
			ptxt "Error"
			stop
		endi

* set some variables
		sets va01 pray agts ov90 "Genetics File" "gren*"
		setv va02 ov00
		setv va50 posx
		setv va51 posy

* create the new egg
		new: simp 3 4 2 "greneggmask" 7 1 10
		elas 10
		fric 100
		attr 195
		bhvr 32
		aero 10
		accg 4
		perm 60
		gene load targ 1 va01
		setv ov01 va02
		mvto va50 va51
		velo -10 -5
		tick 600
		slow
	retn

** Goto next egg agent with grendel genome
	subr next_genome
* set some variables
		setv va99 0
		setv va98 0
		sets va97 ov90
		inst
* find the next agent with a genetics file that starts with gren
		loop
			sets ov90 pray next "EGGS" va97
			sets va97 ov90
			sets va01 pray agts ov90 "Genetics File" ""
			sets va02 subs va01 1 4
			doif va02 eq "gren"
				setv va99 1
			endi
			addv va98 1
		untl va99 eq 1 or va98 gt 100
		slow

* If more than 100 egg agents searched without result, throw error
		doif va98 gt 100
			targ ownr
			sets ov90 ""
			part 1
			ptxt "No genomes"
			stop
		else
			gsub display_name
		endi
	retn

** Goto previous egg agent with grendel genome
	subr prev_genome
		setv va99 0
		setv va98 0
		sets va97 ov90
		inst
* find the previous egg agent with a genome that starts with gren
		loop
			sets ov90 pray prev "EGGS" va97
			sets va97 ov90
			sets va01 pray agts ov90 "Genetics File" ""
			sets va02 subs va01 1 4
			doif va02 eq "gren"
				setv va99 1
			endi
			addv va98 1
		untl va99 eq 1 or va98 gt 100
		slow

* If more than 100 egg agents searched without result, throw error
		doif va98 gt 100
			targ ownr
			sets ov90 ""
			part 1
			ptxt "No genomes"
			stop
		else
			gsub display_name
		endi
	retn


** Gender switch: checks for current gender status and changes it and image accordingly.
	subr set_gender
		doif ov00 eq 2
			setv ov00 1
			part 5
			pose 1
		else
			setv ov00 2
			part 5
			pose 0
		endi
	retn


** Name display function: Displays the breed name on the name screen.
	subr display_name
		sets va01 ov90
		targ ownr
		part 1
		ptxt va01
	retn

endm

****REMOVAL
rscr
enum 3 3 21823
	kill targ
next
scrx 3 3 21823 1001