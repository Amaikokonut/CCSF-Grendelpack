*Now attempts to fly out of water when underwater
*Desert Lance Bug: 2 14 16502
*Desert Lance Bug Egg: 2 18 21845
*Desert Lance Bug Pop Checker: 1 1 21845

* Population checker
*new: simp 1 1 21845 "blnk" 1 0 0
*tick 200


* Desert
*new: simp 1 1 21845 "blnk" 1 0 0
*tick 15
*mvto 22134 3225

* Jungle
*new: simp 1 1 21845 "blnk" 1 0 0
*tick 20
*mvto 19725 779

** Population check timer script

scrp 1 1 21845 9
	setv va37 0
	setv va02 gmap posx posy
* Check how many bugs there are in this metaroom
	inst
	enum 2 14 21845
		doif gmap posx posy eq va02
			addv va37 1
		endi
	next
	enum 2 18 21845
		doif gmap posx posy eq va02
			addv va37 1
		endi
	next
	slow
	doif va37 lt 3
		targ ownr
		setv va00 posx
		setv va01 posy
		tick 3600
		reps 3
*make the first desert lance bugs
			new: simp 2 14 21845 "gp_desertlancebug_CS" 18 0 998
*attr 1 + 2 + 64
			attr 67
*bhvr 1 + 2 + 8 + 16 + 32
			bhvr 59
*can move through most platforms
			perm 10
*move it to a safe test spot
			mvsf va00 va01
*timer: half a second
			tick 10
*animate
			anim [0 1 2 3 4 5 6 7 8 255]
*for movement:
			setv ov10 rand 0 1
			doif ov10 eq 0
				setv ov10 -1
			endi
			setv ov11 rand 0 1
			doif ov11 eq 0
				setv ov11 -1
			endi
		repe
	endi
endm

*push script
scrp 2 14 21845 1
*check if a Creature is pushing
	targ from
	doif fmly eq 4
*Stim the Creature with: Interacted with pest
		stim writ targ 88 1
		snde "stng"
	endi
endm

*pull script
scrp 2 14 21845 2
*check if a Creature is pulling
	targ from
	doif fmly eq 4
*Stim the Creature with: Interacted with pest
		stim writ targ 88 1
		snde "stng"
	endi
endm

*hit script
scrp 2 14 21845 3
*check if a Creature is hitting
	targ from
	doif fmly eq 4
*Stim the Creature with: Interacted with pest
		stim writ targ 88 1
		snde "stng"
	endi
endm

*timer script
scrp 2 14 21845 9
*stop if being carried
	doif carr <> null
		stop
	endi
	addv ov73 rand 1 2
	addv ov74 rand 2 4
	gsub age
	gsub rcec
	gsub repro
*OV12: X speed
*OV13: Y speed
*OV14: Max X speed
*OV15: Max Y speed
*set max speed
	setv ov14 6
	setv ov15 6
*subroutine wall check AND WATER CHECK
	doif rtyp room targ ne 8 and rtyp room targ ne 9
		gsub wall
	else
		setv ov11 -1
	endi
**if not changing X direction because of walls..
	doif va00 ne 1
		doif rand 0 2 eq 0
*change X direction
			negv ov10
		endi
		addv ov90 rand 1 3
	endi
**if not changing Y direction because of walls..
	doif va01 ne 1 and rtyp room targ ne 8 and rtyp room targ ne 9
		doif rand 0 1 eq 0
*change Y direction
			negv ov11
		endi
		addv ov90 rand 2 5
	endi

	gsub move
	gsub anim
*SUBROUTINES
*subroutine: age check	
	subr age
		doif ov73 gt 600
			kill ownr
		endi
	retn
*subroutine: room type check
	subr rcec
*water is bad for these little guys
		doif rtyp room targ eq 8 or rtyp room targ eq 9
*increase age
			addv ov73 rand 15 25
		endi
	retn
*subroutine: reproduce
	subr repro
		doif ov74 gt 180
			rnge 1600
			setv va45 0
			setv va46 0
			setv ov74 0
			setv va55 gmap posx posy
			esee 2 14 21845
				addv va45 1
			next
			esee 2 18 21845
				addv va46 1
			next
			inst
			enum 2 14 21845
				doif gmap posx posy eq va55
					addv va47 1
				endi
			next
			slow
			doif va45 le 2 and va46 le 2 and va47 lt 30
				inst
				setv va90 posx
				setv va91 posy
				new: simp 2 18 21845 "gp_desertlancebug_CS" 1 18 0
				doif tmvt va90 va91 eq 1
					mvto va90 va91
					attr 80
					perm 10
					tick rand 600 1200
				else
					kill targ
				endi
			endi
		endi
		targ ownr
	retn
*subroutine: wall check
	subr wall
*change direction to right
		doif obst 0 lt 100 and ov10 lt 0
			negv ov10
			setv va00 1
		endi
*change direction to left
		doif obst 1 lt 100 and ov10 gt 0
			negv ov10
			setv va00 1
		endi
*change direction to up
		doif obst 2 lt 50 and ov11 lt 0
			negv ov11
			setv va01 1
		endi
*change direction to down
		doif obst 3 lt 100 and ov11 gt 0
			negv ov11
			setv va01 1
		endi
	retn
*subroutine: movement
	subr move
		doif ov10 lt 0
*trying to go left
			negv ov14
			doif ov12 gt ov14
				subv ov12 2
			endi
			negv ov14
		else
*trying to go right
			doif ov12 lt ov14
				addv ov12 2
			endi
		endi

		doif ov11 lt 0
*trying to go down
			negv ov15
			doif ov13 gt ov15
				subv ov13 3
			endi
			negv ov15
		else
*trying to go up
			doif ov13 lt ov15
				addv ov13 3
			endi
		endi
**give velocity
		setv velx ov12
		setv vely ov13
	retn
*subroutine: animate
	subr anim
		inst
		doif ov12 lt 0
			base 9
		elif ov12 gt 0
			base 0
		endi
		slow
	retn
*END SUBROUTINES
endm


*eat script
scrp 2 14 21845 12
*check if being eaten by a Creature
	lock
	targ from
	doif fmly eq 4
		stim writ targ 88 1
		stim writ targ 80 1
		snde "stng"
	endi
*always remove the owner from the world
	kill ownr
endm

*timer script egg
scrp 2 18 21845 9
	targ ownr
	setv va90 posx
	setv va91 posy
	inst
	new: simp 2 14 21845 "gp_desertlancebug_CS" 18 0 998
	attr 67
	bhvr 59
	perm 0
	mvsf va90 va91
	tick 10
	anim [0 1 2 3 4 5 6 7 8 255]
	setv ov10 rand 0 1
	doif ov10 eq 0
		setv ov10 -1
	endi
	setv ov11 rand 0 1
	doif ov11 eq 0
		setv ov11 -1
	endi
	slow
	kill ownr

endm

*remove script
rscr
enum 2 14 21845
	kill targ
next
enum 2 18 21845
	kill targ
next
enum 1 1 21845
	kill targ
next
scrx 2 14 21845 1
scrx 2 14 21845 2
scrx 2 14 21845 3
scrx 2 14 21845 9
scrx 2 14 21845 12
scrx 2 18 21845 9
scrx 1 1 21845 9