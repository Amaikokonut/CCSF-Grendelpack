**
** Door scripts
**

** Check if meso injected
doif game "gpmeso" eq 1
**
** The door inside desert hideout
	new: comp 2 2 21805 "gp_dhelevators_CS" 16 19 1
** Activateable
	attr 4
* The idle animation, teleporting door animation & disable button
	pat: dull 2 "gp_dhelevators_CS" 35 92 59 1
	pat: dull 3 "gp_dhelevators_cs" 18 0 0 2
	pat: butt 4 "gp_dhelevators_CS" 40 2 113 83 1 [] 500 1
* The disabled OV
	setv ov00 0
* lift id
	setv ov01 0
** The idle animation
	part 2
	frat 2
	anim [0 0 0 0 0 0 0 0 0 4 3 2 1 255]
** the frametick of the teleporting animation
	part 3
	frat 2
	mvto 22532 3524
**
** the door inside meso
	new: comp 2 2 21805 "gp_doordhmeso_CS" 28 9 1
** Activateable
	attr 4
* The idle animation, teleporting door animation & disable button
	pat: dull 2 "gp_doordhmeso_CS" 0 0 0 1
	pat: dull 3 "gp_doordhmeso_CS" 9 0 0 2
	pat: butt 4 "gp_doordhmeso_CS" 26 2 54 99 1 [1 0 255] 500 1
* The disabled OV
	setv ov00 0
* lift id
	setv ov01 1
** The idle animation
	part 2
	frat 4
	anim [2 4 0 0 0 6 7 8 255]
** the frametick of the teleporting animation
	part 3
	frat 2
	part 4
	frat 5
	anim [0]
	mvto 23520 440
endi
**
** Shortcut doors
** If main injected, create doors to/from main
doif game "gpmain" eq 1
** Door main > desert hideout
	new: comp 2 2 21808 "gp_shortcutdoors_CS" 3 2 100
	pat: butt 2 "gp_shortcutdoors_CS" 0 2 18 57 0 [] 1004 0
	setv ov00 1
	setv ov01 0
	attr 4
	bhvr 1
	mvto 22010 1091
** Door desert hideout > main
	new: comp 2 2 21808 "gp_shortcutdoors_CS" 3 2 100
	pat: butt 2 "gp_shortcutdoors_CS" 0 2 18 57 0 [] 1004 0
	setv ov00 1
	setv ov01 0
	attr 4
	bhvr 1
	mvto 21643 3665
endi

**
** Shortcut teleporters
** If neither meso or main injected, create doors to/from capillata
doif game "gpmain" ne 1 and game "gpmeso" ne 1
** Door capillata > desert hideout
	new: comp 2 2 21808 "gp_radioteleport" 3 0 100
	setv ov00 50
	setv ov01 0
	attr 198
	perm 100
	fric 100
	aero 1
	accg 3
	elas 0
	bhvr 1
	frat 3
	mvto 22363 3566
** Door desert hideout > main
	new: comp 2 2 21808 "gp_radioteleport" 3 0 100
	setv ov00 50
	setv ov01 0
	attr 198
	perm 100
	fric 100
	aero 1
	accg 3
	elas 0
	frat 3
	bhvr 1
	mvsf game "CreatorX" game "CreatorY"
endi



**
** SCRIPTS
**

**
** Door script
**

** Push
scrp 2 2 21805 1
** check if the door is enabled
	doif ov00 eq 0
** If yes, lock the script
		lock

** Search the other door
		seta va90 ownr
		inst
		enum 2 2 21805
			doif targ ne ownr
				seta va90 targ
			endi
		next
		slow
** Check whether the other door is different from this one
		doif va90 ne ownr
** Target the other door to set coordinates
			targ va90
			setv va05 posx
			setv va06 posy
			targ ownr
** Opening animation sequence

			part 3
			plne 2
			doif ov01 eq 0
				anim [2 3 4 5 6 7 8 9 10 11 12 13 14 15]
			else
				anim [1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16]
			endi
			over
			anim [14 13 0]
			targ va90
			part 3
			plne 2
			anim [15 14 13 12 11 10 9 8 7 6 5 4 3 2 1 0]
			doif from eq pntr
				cmrp va05 va06 0
			else
				doif fmly eq 4
** Target the pusher
					targ from
** Move the creature
					mvft va05 va06
** Stim the creature "travelled through door"
					stim writ from 95 1
** urge to remove attention from door
** and reset the verb state to neutral
					urge writ va00 2 -1 -1 1
				endi

			endi

		endi
	endi

endm

** Disable
scrp 2 2 21805 500
	mesg writ pntr 101
	doif ov00 eq 0
		doif ov01 eq 0
			pose 15
		else
			part 4
			anim [1]
		endi
		part 2
		pose 0
		attr 16
		setv ov00 1
	else
		doif ov01 eq 0
			pose 0
			part 2
			anim [0 0 0 0 0 0 0 0 0 4 3 2 1 255]

		else
			part 4
			anim [0]
			part 2
			anim [2 4 0 0 0 6 7 8 255]

		endi
		attr 4
		setv ov00 0
	endi
endm



rscr

enum 2 2 21805
	kill targ
next