**
** Crayfish tail vendor
**
** Sprites by Jesseth, coding by Pilla.
**
**
** Feel free to use this code however you want.
**
**

**
** Caos2pray chunk
**
** Compiles the agent.
**
* CAOS2PRAY CHUNK
*# Pray-File "gp_crayfishtail_vendor_JESS.agents"
*# DS-Name "Crayfish vendor"
*# attach gp_crayfishtail_vendor_JESS.c16 GrenP_CrayfishtailVendor.catalogue
*# desc = "A vendor that vends crayfish tails. Well, at least one of the tails might be crayfish...\nSprites by Jesseth."
*# Agent Animation File = "gp_crayfishtail_vendor_JESS.c16"
*# Agent Sprite First Image = 0
*# Agent Animation Gallery = "gp_crayfishtail_vendor_JESS"
*
*

*
** Injection script
**

**
** The vendor
**

new: comp 2 23 21826 "gp_crayfishtail_vendor_JESS" 1 0 rand 800 900
mesg wrt+ targ 1000 game "CreatorX" game "CreatorY" 0

**
** SCRIPTS
**
**
**

**
** Creator script
**

** Create a vendor
scrp 2 23 21826 1000
** Check if using secondary base
	doif ov60 eq 1
		new: comp 2 23 21826 "gp_crayfishtail_vendor_JESS" 1 17 rand 800 900
** The aquarium
		pat: dull 2 "gp_crayfishtail_vendor_JESS" 18 0 4 1
** The steamed tail button
		pat: butt 3 "gp_crayfishtail_vendor_JESS" 24 2 14 39 1 [1 1 255] 500 0
** The other button
		pat: butt 4 "gp_crayfishtail_vendor_JESS" 26 2 46 39 2 [1 1 255] 501 0
	else
		new: comp 2 23 21826 "gp_crayfishtail_vendor_JESS" 1 0 rand 800 900
** The aquarium
		pat: dull 2 "gp_crayfishtail_vendor_JESS" 1 0 4 1
** The steamed tail button
		pat: butt 3 "gp_crayfishtail_vendor_JESS" 7 2 14 39 1 [1 255] 500 0
** The other button
		pat: butt 4 "gp_crayfishtail_vendor_JESS" 9 2 46 39 2 [1 255] 501 0
	endi
** Attributes: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
	perm 100
	accg 3
	aero 1
	fric 80
	elas 20
** behaviour: act1 + hit 8 = 9
	bhvr 9
** emit a tiny bit of food smell
	emit 8 0.05

	part 2
	frat 7
	anim [ 0 1 2 3 4 5 12 13 14 15 12 13 14 15 255 ]
	tick rand 1000 10000
	part 3
	anim [0 0 255]
	part 4
	anim [0 0 255]
	mvsf _p1_ _p2_
	targ ownr
	kill ownr
endm



**
** VENDOR
**

*
** TIMER SCRIPT

scrp 2 23 21826 9
	doif movs eq 0
		mesg writ ownr 0
		tick rand 1000 10000
	endi
endm

*
** HIT SCRIPT
scrp 2 23 21826 3
	stim writ from 92 1
endm

*
** Creature push

scrp 2 23 21826 1
	doif from eq pntr
		mesg writ pntr 101
	else
		stim writ from 90 1
	endi

	setv va00 rand 0 1
	setv va05 posl
	setv va06 post

	lock

	setv va07 va00
	addv va07 3
** animate the button
	part va07
	frat 3
	anim [1 0 1 0]
	over
	anim [0 0]

** Vend tail
	targ ownr

	addv va05 41
	addv va06 59

	setv va07 va00
	addv va07 11

	setv va08 va00
	addv va08 11

	inst
	new: simp 2 va08 21826 "gp_crayfishtail_vendor_JESS" 2 va07 rand 2000 2200
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr eat 16 + pickup 32 = 48
	attr 194
	bhvr 48
** won't go through walls
	perm 100
** Physics
	accg 0.6
	aero 1
	fric 25
	elas 40
** set category to "food"
	cato 11
** emit fat
	emit 8 0.6
** tick before disappearing
	tick rand 1200 1800
	mvto va05 va06
	unlk

endm


** button1 push

scrp 2 23 21826 500
	doif from eq pntr
		mesg writ pntr 101
	endi

	setv va05 posl
	setv va06 post

	lock

	part 3
	frat 3
	anim [1 0 1 0]
	over
	anim [0 0]


** Vend tail
	targ ownr

	addv va05 41
	addv va06 57

	inst
	new: simp 2 11 21826 "gp_crayfishtail_vendor_JESS" 2 11 rand 2000 2200
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr eat 16 + pickup 32 = 48
	attr 194
	bhvr 48
** won't go through walls
	perm 100
** Physics
	accg 0.6
	aero 1
	fric 25
	elas 40
** set category to "food"
	cato 11
** emit fat
	emit 8 0.6
** tick before disappearing
	tick rand 1200 1800
	mvto va05 va06
	unlk

endm

** button2 push

scrp 2 23 21826 501
	doif from eq pntr
		mesg writ pntr 101
	endi

	setv va05 posl
	setv va06 post

	lock

	part 4
	frat 3
	anim [1 0 1 0]
	anim [0 0]
	over

** Vend some tail
	targ ownr

	addv va05 41
	addv va06 59

	inst
	new: simp 2 12 21826 "gp_crayfishtail_vendor_JESS" 2 12 rand 2000 2200
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr eat 16 + pickup 32 = 48
	attr 194
	bhvr 48
** won't go through walls
	perm 100
** Physics
	accg 0.6
	aero 1
	fric 25
	elas 40
** set category to "food"
	cato 11
** emit fat
	emit 8 0.6
** tick before disappearing
	tick rand 1200 1800
	mvto va05 va06
	unlk

endm




** FOOD
* 

** Shrimptail eat script
scrp 2 11 21826 12
	lock
	wait 5
	stim writ from 79 1
	wait 5
	kill ownr
	unlk
endm

** Shrimptail timer script
scrp 2 11 21826 9
	doif carr eq null
		kill ownr
	endi
endm


** Othertail eat script
scrp 2 12 21826 12
	lock
	wait 5
	stim writ from 79 1
	wait 5
	kill ownr
	unlk
endm

** Othertail timer script
scrp 2 12 21826 9
	doif carr eq null
		kill ownr
	endi
endm


*
** Removal script 
*

rscr

enum 2 23 21826
	kill targ
next
enum 2 4 21826
	kill targ
next
enum 2 11 21826
	kill targ
next
enum 2 12 21826
	kill targ
next

scrx 2 23 21826 9
scrx 2 23 21826 3
scrx 2 23 21826 1
scrx 2 11 21826 9
scrx 2 11 21826 12
scrx 2 12 21826 9
scrx 2 12 21826 12