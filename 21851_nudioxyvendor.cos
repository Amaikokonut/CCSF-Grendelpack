
**
** Nudibranch vendor script
**
** Sprites by Jesseth, coding by Pilla.
**
**
** Feel free to use this code however you want.
**
**

**
** Caos2pray chunk
**
** Compiles the agent.
**
* CAOS2PRAY CHUNK
*# Pray-File "gp_nudioxyvendor_JESS.agents"
*# DS-Name "GP - Nudi-Oxy vendor"
*# attach gp_nudioxy_JESS.c16 
*# depend blnk.c16
*# desc = "Cute nudibranches and fish to liven up your aquatic areas"
*# Agent Animation File = "gp_nudioxy_JESS.c16"
*# Agent Sprite First Image = 32
*# Agent Animation Gallery = "gp_nudioxy_JESS"
*
*

*
** Injection script
**
new: comp 2 23 21851 "gp_nudioxy_JESS" 12 0 rand 800 900
mesg wrt+ targ 1000 game "CreatorX" game "CreatorY" 0

**
** Nudioxy Vendor creator script
**


scrp 2 23 21851 1000
	new: comp 2 23 21851 "gp_nudioxy_JESS" 12 0 rand 800 3000
** Set vendor default animation
	frat 5
	anim [5 6 7 2 8 9 3 255]
** Attributes: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
** bhvr: act1 1 + hit 8 = 9
	bhvr 9
** Physics
	perm 100
	accg 3
	aero 1
	fric 80
	elas 20

** part 4: oxy button
	pat: butt 4 "gp_nudioxy_JESS" 12 2 0 0 1 [1] 500 1
** part 5: nudibranch
	pat: butt 5 "gp_nudioxy_JESS" 14 2 0 0 1 [1] 501 1

** Set button default animations
	part 4
	anim [0]
	part 5
	anim [0]

	mvsf _p1_ _p2_
	targ ownr
	kill ownr
endm

**
**
** VENDOR
**
**

*
** VEND OXYMONACANTHUS
scrp 2 23 21851 500
	setv va00 posx
	subv va00 25
	setv va01 post
	new: simp 2 16 21853 "gp_nudioxy_JESS" 20 38 1000
	mesg wrt+ targ 1000 va00 va01 0
endm
*
** VEND NUDIBRANCH
scrp 2 23 21851 501
	setv va00 posx
	addv va00 14
	setv va01 post
	subv va01 20
	new: simp 2 15 21851 "gp_nudioxy_JESS" 24 17 rand 2000 3000
	velo rand -3 3 -10
	mesg wrt+ targ 1000 va00 va01 0
endm

*
** HIT SCRIPT
scrp 2 23 21851 3
	stim writ from 92 1
endm

*
** PUSH SCRIPT

scrp 2 23 21851 1
	lock
	doif from eq pntr
		mesg writ pntr 101
	else
		stim writ from 90 1
	endi
	inst
	doif rand 0 1 eq 0
		mesg writ ownr 501
	else
		mesg writ ownr 500
	endi
	slow
	unlk
endm


*
** Removal script 
*

rscr

** Vendor
enum 2 23 21851
	kill targ
next



scrx 2 23 21851 9
scrx 2 23 21851 3
scrx 2 23 21851 1