**
** Licorice Quill script
**
** Sprites by ChameleonSushi, coding by Pilla.
**
**
** Feel free to use this code however you want.
**
**

**
** Caos2pray chunk
**
** Compiles the agent.
**
* CAOS2PRAY CHUNK
*# Pray-File "gp_licoricequill_CS.agents"
*# DS-Name "GP - Licorice Quill"
*# attach gp_licorice_quill_CS.c16 GrenP_LicoriceQuil.catalogue
*# desc = "Yay, it's a licorice quill. When dispensed, a cute seed packet will appear.\n\nSprites by CosmiSynth"
*# Agent Animation File = "gp_licorice_quill_CS.c16"
*# Agent Sprite First Image = 6
*# Agent Animation Gallery = "gp_licorice_quill_CS"
*
*

*
** Injection script
**

**
** The seed packet!
**

new: simp 2 23 21804 "gp_licorice_quill_CS" 1 14 rand 1000 1500
mesg wrt+ targ 1000 game "CreatorX" game "CreatorY" 0

**
** SCRIPTS
**
**


**
** Creator script
**

* Seed
scrp 2 3 21804 1000
inst
	new: simp 2 3 21804 "gp_licorice_quill_CS" 1 0 rand 1000 1500
** attr: mouseable 2 + collide 64 + physics 128 = 194** bhvr: pickup 32 & eat 16 = 48
			attr 194
			bhvr 48
			perm 60
** Physics
			accg 1
			fric 70
			elas 40
** Emit starchy smell
			emit 7 0.8
** launch seed
			velo rand -7 7 -2
			tick rand 1 500
			slow
	mvsf _p1_ _p2_
	targ ownr
	kill ownr
endm

* Seed packet
scrp 2 23 21804 1000
inst
	new: simp 2 23 21804 "gp_licorice_quill_CS" 1 14 rand 1000 1500
** attr: mouseable 2 + activateable 4 + invisible 16 + collide 64 + physics 128 = 210
	attr 214
** Will go through some walls
	perm 60
** Physics
	accg 1
	aero 1
	fric 70
	elas 20
	mvsf _p1_ _p2_
	targ ownr
	kill ownr
slow
endm

**
** Population control
**

*
** Licorice quill population control
scrp 1 1 21804 9
	tick rand 1500 8000
	setv va00 0
	esee 2 4 21804
		addv va00 1
	next
	doif va00 le 2
		setv va02 posx
		setv va03 posy
		reps 3
			inst
			new: simp 2 3 21804 "gp_licorice_quill_CS" 1 0 rand 1000 1500
** attr: mouseable 2 + collide 64 + physics 128 = 194
** bhvr: pickup 32 & eat 16 = 48
			attr 194
			bhvr 48
			perm 60
** Physics
			accg 1
			fric 70
			elas 40
** Emit starchy smell
			emit 7 0.8
** launch seed
			velo rand -7 7 -2
			tick rand 1 500
			mvto va02 va03
			slow
		repe
	endi
endm


*
** Seed packet push script
*
scrp 2 23 21804 1
	doif from eq pntr
		mesg writ pntr 101
	endi
	setv va01 posx
	setv va02 posy
	inst
	reps 8
		new: simp 2 3 21804 "gp_licorice_quill_CS" 1 0 rand 1000 1500
** attr: mouseable 2 + collide 64 + physics 128 = 194
		attr 194
** Will go through some walls
		perm 60
** Physics
		accg 1
		aero 1
		fric 70
		elas 40
** bhvr: pickup 32 & eat 16 = 48
		bhvr 48
** Emit starchy smell
		emit 7 0.8
** launch seed
		velo rand -4 4 -3
		tick rand 400 900
		mvto va01 va02
	repe

	targ ownr
	kill ownr

endm


*
** Plant growing/seeding script
*

** TIMER
scrp 2 4 21804 9
** Check if the plant is about to start growing: if yes, check if there aren't any other plants in this spot.
	doif tick eq 2
** Check if there's enough space to grow: if not, kill the plant placeholder
		inst
		setv va00 0
		rnge 300
		esee 2 4 21804
			addv va00 1
		next
		doif va00 ge 3
			kill ownr
			stop
		endi
** Set the plant growing timer
		tick rand 70 100
		slow
	endi
** Check if the plant is fully grown. If not, grooww
	doif pose lt 6
		setv va00 pose
		addv va00 1
		pose va00
	elif pose eq 6
		setv va00 pose
		addv va00 1
		pose va00
		tick rand 2300 4000
** If the plant has lived a happy and long life... set seed and die. Goodbye plant.
	elif pose gt 6
		pose 8
		setv va01 posl
		addv va01 0
		setv va02 post
		addv va02 10
		inst
		new: simp 2 3 21804 "gp_licorice_quill_CS" 1 0 rand 1000 1500
** attr: mouseable 2 + collide 64 + physics 128 = 194
		attr 194
** Will go through some walls
		perm 60
** Physics
		accg 0.05
		aero 1
		fric 70
		elas 60
** bhvr: pickup 32 & eat 16 = 48
		bhvr 48
** Emit starchy smell
		emit 7 0.8
** launch seed
		velo rand -7 7 -3
		tick rand 400 900
		mvto va01 va02


		addv va01 11
		addv va02 -6


		new: simp 2 3 21804 "gp_licorice_quill_CS" 1 0 rand 1000 1500
** attr: mouseable 2 + collide 64 + physics 128 = 194
		attr 194
** Will go through some walls
		perm 60
** Physics
		accg 0.05
		aero 1
		fric 30
		elas 60
** bhvr: pickup 32 & eat 16 = 48
		bhvr 48
** Emit starchy smell
		emit 7 0.8
** launch seed
		velo rand -7 7 -3
		tick rand 400 900
		mvto va01 va02


		addv va01 2
		addv va02 8


		new: simp 2 3 21804 "gp_licorice_quill_CS" 1 0 rand 1000 1500
** attr: mouseable 2 + invisible 16 + collide 64 + physics 128 = 210
		attr 210
** Will go through some walls
		perm 60
** Physics
		accg 0.05
		aero 1
		fric 30
		elas 60
** launch seed
		velo rand -7 7 -3
		tick rand 200 500
		mvto va01 va02

		slow
** Retarget the plant
		targ ownr
** Kill it ;u;
		tick 0
		lock
		frat 50
		anim [8 9 10 11 12]
		over
		kill ownr
		unlk
	endi


endm


*
** Seed timer & eat script
*


*
** EAT
scrp 2 3 21804 12
** Lock the script, so once someone's eating the seed, it can't be interrupted.
	lock
** Wait, so the creatures can't overeat :p
	wait 25
** Stim the eating creature with "eaten plant"
	stim writ from 77 1
** Wait a little more :p
	wait 25
** Kill the seed
	kill ownr
endm


*
** TIMER
scrp 2 3 21804 9

** Get the room type of the seed
	setv va00 posl
	setv va01 post
	setv va02 grap va00 va01
	setv va03 rtyp va02

** only survive: drained soil 7, normal soil 5, atmosphere 1
	doif carr eq null
		doif va03 ne 7 and va03 ne 5 and va03 ne 1
			kill ownr
			stop
		endi
	endi

** Check if the seed has settled
	doif carr eq null and fall eq 0
		doif va03 eq 7 or va03 eq 5
			setv va00 posl
			setv va01 posb
			subv va00 10
			subv va01 23
			inst

** Grow plant!
			new: simp 2 4 21804 "gp_licorice_quill_CS" 15 1 rand 1000 3000
** attr: invisible 16, bhvr: 0
			attr 16
			bhvr 0
			mira rand 0 1
** Set the tick to 2 to trigger the 'do I have enough space to grow' check script
			tick 2
** test-move it into the space: if it can't grow there, kill it
			doif tmvt va00 va01 eq 0
				kill targ
			else
** If it can grow here, grow!
				mvto va00 va01
			endi
		endi
		slow
		targ ownr
		kill ownr
	endi
endm


*
*
** Removal script 
*
*

rscr


enum 2 23 21804
	kill targ
next
enum 2 3 21804
	kill targ
next
enum 2 4 21804
	kill targ
next


scrx 2 23 21804 1
scrx 2 3 21804 9
scrx 2 4 21804 9