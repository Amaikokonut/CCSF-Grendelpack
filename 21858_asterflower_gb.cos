**
** Aster Flower Script
**
** Sprites by Cerulean, coding by Pilla.
**
**
** Feel free to use this code however you want.
**
**

**
** Caos2pray chunk
**
** Compiles the agent.
**
* CAOS2PRAY CHUNK
*# Pray-File "gp_gb_aster.agent"
*# DSGB-Name "Aster Flower"
*# Attach gp_aster.c16

*# desc = "This is an aster flower, its job is to look pretty."
*# Agent Animation File = "gp_aster.c16"
*# Agent Animation Gallery = "gp_aster"
*# Agent Sprite First Image = 16
*# GB_Category = 2
*# Agent Author = "Cerulean"
*

*
** Injection script
**

**
** Vend a Gardenbox seed
**

** Run this script instantly, so it can't be interrupted by other scripts.
inst
* Create a new simple object (the seed).
* It uses the "gp_aster" sprite. It contains 1 image, the 0th image.
* The plane is something random between 500 and 1500. (behind creatures)
new: simp 2 3 21858 "gp_aster" 1 0 rand 500 1500
** attr: mouseable 2 + invisible 16 + collide 64 + physics 128 = 210
attr 210
** Will go through some walls (higher number -> less walls)
perm 60
** Physics
accg 0.1
aero 1
fric 90
elas 20
** Give the seed an initial tick, before it'll check whether it can grow.
tick rand 50 200
** Move to the gardenbox crosshair x and y coordinates
mvsf game "ch_x" game "ch_y"
slow


**
** SCRIPTS
**
**

*
** Seed timer & eat script
*

*
** EAT
scrp 2 3 21858 12
** Lock the script, so once someone's eating the seed, it can't be interrupted.
	lock
** Wait, so the creatures can't overeat :p
	wait 25
** Stim the eating creature with "eaten plant"
	stim writ from 77 1
** Wait a little more :p
	wait 25
** Kill the seed
	kill ownr
endm


*
** TIMER
scrp 2 3 21858 9
** Get the room type of the seed
	setv va00 posl
	setv va01 post
	setv va02 grap va00 va01
	setv va03 rtyp va02

** only survive: drained soil 7, boggy soil 6, normal soil 5, atmosphere 1
	doif carr eq null
		doif va03 ne 7 and va03 ne 5 and va03 ne 1 and va03 ne 6
			kill ownr
			stop
		endi
	endi

** Check if the seed has settled
	doif carr eq null and fall eq 0
		doif va03 eq 7 or va03 eq 5
			subv va00 5
			subv va01 25
			inst

** Create a new aster plant.
** It uses the gp_aster sprite, it has 26 frames and it starts at frame 1.
			new: simp 2 4 21858 "gp_aster" 26 1 rand 1000 3000
** attr: invisible 16, bhvr: 0. You could technically make them visible too, but I think it distracts creatures too much. :P
			attr 16
			bhvr 0
** Set the tick to 2 to trigger the 'do I have enough space to grow' check script
			tick 2
** test-move it into the space: if it can't grow there, kill it
			doif tmvt va00 va01 eq 0
				kill targ
			else
** If it can grow here, grow!
				mvto va00 va01
			endi
		endi
		slow
** Retarget the original seed
		targ ownr
** Kill the seed, since it's now turned into a plant.
		kill ownr
	endi
endm



*
** Plant script
*

** TIMER (to grow & make seed)
scrp 2 4 21858 9
** Check if the flower is about to start growing: if yes, check if there aren't any other plants in this spot.
	doif tick eq 2
** Check if there's enough space to grow: if not, kill the plant placeholder
		inst
		setv va00 0
		rnge 200
		esee 2 4 21858
			addv va00 1
		next
		doif va00 ge 4
			kill ownr
			stop
		endi

** Check if you're not touching other aster flowers
		setv va00 0
		etch 2 4 21858
			addv va00 1
		next
		doif va00 ge 2
			kill ownr
			stop
		endi

** Set the plant growing timer
		tick rand 80 120
		slow
	endi
	setv va00 pose
** Check if the plant is fully grown. If not, grooww
	doif va00 lt 15
		addv va00 1
		pose va00
** If the plant is fully grown, wait a little longer before dying
	elif va00 eq 15
		tick rand 120 180
		addv va00 1
		pose va00
** If the plant has lived a happy and long life... set seed and die. Goodbye plant.
	else
		tick 0
** Create the seed
** Get positions to spawn the seed
		setv va01 posl
		addv va01 1
		setv va02 post
		addv va02 6
** Run the next part in an inst
		inst
** Make 2 seed that are visible to creatures
		reps 2
			inst
** New: simple, same seed as the one you inject above
			new: simp 2 3 21858 "gp_aster" 1 0 rand 1000 1500
** attr: mouseable 2 + collide 64 + physics 128 = 194
			attr 194
** Will go through some walls
			perm 60
** Physics
			accg 0.1
			aero 1
			fric 90
			elas 20
** bhvr: pickup 32 & eat 16 = 48
			bhvr 48
** Emit starchy smell
			emit 7 0.8
** launch seed
			velo rand -20 20 -7
			tick rand 50 200
			mvto va01 va02
			slow
		repe

** Create two invisible seed (to keep the population from dying
		reps 2
			inst
			new: simp 2 3 21858 "gp_aster" 1 0 rand 1000 1500
** attr: mouseable 2 + invisible 16 + collide 64 + physics 128 = 210
			attr 210
** Will go through some walls
			perm 60
** Physics
			accg 0.1
			aero 1
			fric 90
			elas 20
** launch seed
			velo rand -20 20 -7
			tick rand 50 200
			mvto va01 va02
			slow
		repe
** Retarget the plant
		targ ownr
		lock
** Dying animation
		frat 10
		anim [16 17 18 19 20 21 22 23 24]
		over
** Kill the plant
		kill ownr
		unlk
	endi
endm



*
*
** Removal script 
*
*

rscr

enum 2 3 21858
	kill targ
next
enum 2 4 21858
	kill targ
next
scrx 2 3 21858 9
scrx 2 4 21858 9