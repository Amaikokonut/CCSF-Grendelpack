**
** Door scripts
**

** Check if main injected
*doif game "gpmain" eq 1 and game "gpgrendelhideout" ne 1
**
** The door inside the grendel hideout
new: comp 2 2 21857 "gp_doormesojungle_UZAG" 11 26 1
** Activateable
attr 4
* The teleporting door animation & disable button
pat: dull 3 "gp_doormesojungle_UZAG" 37 0 0 2
pat: butt 4 "gp_doormesojungle_UZAG" 74 2 143 68 1 [] 500 0
part 4
pose 1
* The disabled OV
setv ov00 0
* lift id
setv ov01 0
mvto 20181 811
**
** the door inside main
new: comp 2 2 21857 "gp_doormesojungle_UZAG" 11 26 1
** Activateable
attr 4
* The teleporting door animation & disable button
pat: dull 3 "gp_doormesojungle_UZAG" 37 0 0 2
pat: butt 4 "gp_doormesojungle_UZAG" 74 2 143 78 1 [] 500 0
part 4
pose 1
* The disabled OV
setv ov00 0
* lift id
setv ov01 0
mvto 21180 2190
*endi

**
** SCRIPTS
**

**
** Door script
**

** Push
scrp 2 2 21857 1
** check if the door is enabled
	doif ov00 eq 0
** If yes, lock the script
		lock

** Search the other door
		seta va90 ownr
		inst
		enum 2 2 21857
			doif targ ne ownr
				seta va90 targ
			endi
		next
		slow
** Check whether the other door is different from this one
		doif va90 ne ownr
** Target the other door to set coordinates
			targ va90
			setv va05 posx
			setv va06 posy
			targ ownr
** Opening animation sequence

			part 3
			doif ov01 eq 0
				anim [1 2 3 4 5 6]
				over
				anim [7 8 9 10 11 0]
			endi


			targ va90
			part 3
			doif ov01 eq 0
				anim [5 4 3 2 1 0]
			endi

			doif from eq pntr
				cmrp va05 va06 0
			else
				doif fmly eq 4
** Target the pusher
					targ from
** Move the creature
					mvft va05 va06
** Stim the creature "travelled through door"
					stim writ from 95 1
** urge to remove attention from door
** and reset the verb state to neutral
					urge writ va00 2 -1 -1 1
				endi

			endi

		endi
	endi

endm

** Disable
scrp 2 2 21857 500
	mesg writ pntr 101
	doif ov00 eq 0
		part 0
		doif ov01 eq 0
			anim [1 2 3 4 5 6 7 8 9 10 11]
		else
			anim [1 2 3 4 5 6 7 8 9 10]
		endi
		part 4
		pose 0
		attr 16
		setv ov00 1
	else
		part 0
		doif ov01 eq 0
			anim [10 9 8 7 6 5 4 3 2 1 0]
		else
			anim [9 8 7 6 5 4 3 2 1 0]
		endi
		part 4
		pose 1
		attr 4
		setv ov00 0
	endi
endm



rscr

enum 2 2 21857
	kill targ
next