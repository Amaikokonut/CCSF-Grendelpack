**
** Pickleweed plant script
**
** Sprites by Jesseth, coding by Pilla.
**
**
** Feel free to use this code however you want.
**
**

**
** Caos2pray chunk
**
** Compiles the agent.
**
* CAOS2PRAY CHUNK
*# Pray-File "gp_pickleweed_plant_JESS.agents"
*# DS-Name "Pickleweed Plant (Jesseth)"
*# attach gp_pickleweed_plant_JESS.c16 
*# desc = "Some pretty pickleweed."
*# Agent Animation File = "gp_pickleweed_plant_JESS.c16"
*# Agent Sprite First Image = 20
*# Agent Animation Gallery = "gp_pickleweed_plant_JESS"
*
*

*
** Injection script
**

**
** The pickleweed mother
**

*new: comp 2 23 21820 "gp_pickleweed_plant_JESS" 1 0 rand 800 900
*mesg wrt+ targ 1000 game "CreatorX" game "CreatorY" 0

**
** SCRIPTS
**
**
**

**
** Creator
**

scrp 2 23 21820 1000
	new: comp 2 23 21820 "gp_pickleweed_plant_JESS" 1 0 rand 800 900
** Attributes: mouseable 2 + activateable 4 + invisible 16 + collide 64 + physics 128 = 214
	attr 214
	perm 100
	accg 1
	aero 1
	fric 80
	elas 20
** bhvr: act1 1 + hit 8 = 9
	bhvr 9
** Create the spawning spots
	pat: dull 2 "gp_pickleweed_plant_JESS" 1 7 32 1
	pat: dull 3 "gp_pickleweed_plant_JESS" 1 20 1 1
	pat: dull 4 "gp_pickleweed_plant_JESS" 1 42 16 1
	pat: dull 5 "gp_pickleweed_plant_JESS" 1 63 19 1
	tick 20
	mvsf _p1_ _p2_
	targ ownr
	kill ownr
endm

**
** Population monitor
**


*
** Pickleweed population control 
scrp 1 1 21820 9
	tick rand 1500 8000
	setv va00 0
	esee 2 4 21820
		addv va00 1
	next
	doif va00 le 1
		setv va02 posx
		setv va03 posy
		reps 3
			inst
			new: simp 2 3 21820 "gp_pickleweed_plant_JESS" 4 10 rand 800 900
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr eat 16 + pickup 32 = 48
			attr 194
			bhvr 48
			accg 1
			fric 25
			elas 30
			tick rand 1 500
			velo rand -8 8 0
			mvsf va02 va03
			slow
		repe
	endi
endm


**
** PICKLEWEED MOTHER
**


*
** hit
scrp 2 23 21820 3
	stim writ from 92 1
endm


*
** PUSH SCRIPT

scrp 2 23 21820 1

	doif from eq pntr
		mesg writ pntr 101
	else
		stim writ from 90 1
	endi

	setv va05 posx
	setv va06 posy
	new: simp 2 3 21820 "gp_pickleweed_plant_JESS" 4 10 rand 2800 2900
** attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
** Will go through some walls
	perm 80
** Physics
	accg 0.4
	aero 1
	fric 25
	elas 60
	tick rand 700 1400
	velo rand -3 3 rand -2 0
	mvto va05 va06
endm


*
** TIMER SCRIPT

scrp 2 23 21820 9
	doif movs eq 0

** Reset the tick
		tick 0

** Grow the flowers and spawn the seed
		setv va01 2
		reps 4
			setv va07 rand 0 3
			part va01

** pose ge 9 --> fully grown, drop a seed now
			doif pose ge 9
				inst
** reset seed lobe
				pose 0
** Create and drop a new seed
				setv va05 posl
				setv va06 post

				doif va01 eq 2
					addv va05 7
					addv va06 32
				elif va01 eq 3
					addv va05 20
					addv va06 1
				elif va01 eq 4
					addv va05 42
					addv va06 16
				else
					addv va05 63
					addv va06 19
				endi

				new: simp 2 3 21820 "gp_pickleweed_plant_JESS" 4 10 rand 800 900
** attr: mouseable 2 + collide 64 + physics 128 = 194
				attr 194
** Will go through some walls
				perm 80
** Physics
				accg 0.4
				aero 1
				fric 25
				elas 60
				tick rand 700 1400
				mvto va05 va06
				slow

** pose ge 5 --> grow to full stage
			elif pose ge 5
				addv va07 9
				pose va07

** pose ge 1 --> grow to second stage
			elif pose ge 1
				addv va07 5
				pose va07
			endi
			addv va01 1
			targ ownr
		repe

** Check how many seed lobes there are currently growing
		setv va00 0
		setv va01 2
		reps 4
			part va01
			doif pose ne 0
				addv va00 1
			endi
			addv va01 1
		repe

** If there are less than 2 seed lobes growing, there may grow another one.
		doif va00 lt 2
			setv va03 0
** Make the first checking point random.
			setv va01 2
			addv va01 rand 0 3

** If the first spot isn't free, try the next one.
			reps 4
				part va01
				doif pose eq 0 and va03 eq 0
					addv va03 rand 1 4
					pose va03
				endi
				addv va01 1
				doif va01 gt 5
					setv va01 2
				endi
			repe
		endi

		targ ownr
		tick rand 700 1200
	endi
endm


** PLANT
*

** plant growing timer

scrp 2 4 21820 9
** Check if the plant is about to start growing: if yes, check if there aren't too many other plants around.
	doif tick eq 2
** Check if there's enough space to grow: if not, kill the plant placeholder
		inst
		setv va00 0
		rnge 40
		esee 2 4 21820
			addv va00 1
		next

		doif va00 ge 1
			kill ownr
			stop
		endi

		setv va00 0
		rnge 210
		esee 2 4 21820
			addv va00 1
		next
		doif va00 ge 2
			kill ownr
			stop
		endi
** Set the plant growing timer
		tick rand 160 300
		slow
	endi
** Check if the plant is fully grown. If not, grooww
	doif pose lt 6
		setv va00 pose
		addv va00 1
		pose va00
** If the plant is fully grown
	elif pose eq 6
** check how many times a seed has been dropped: if more than 3 times kill the plant.
		doif ov00 gt 3
			targ ownr
** Kill it ;u;
			lock
			part 2
			pose 0
			part 3
			pose 0
			targ ownr
			part 0
			frat 40
			anim [4 4 4 2 2 2]
			over
			kill ownr
			stop
			unlk

** If less than/equal to 3 times, do the grow subroutines

		else

** Make/grow seed
			setv va01 2
			reps 2
				setv va07 rand 0 3
				part va01

** seed part pose
				doif pose ge 9
					inst
					pose 0

					setv va05 posl
					setv va06 post

					doif va01 eq 2
						addv va05 11
						addv va06 0
					else
						addv va05 33
						addv va06 14
					endi

					addv ov00 1
** Create the seed

					new: simp 2 3 21820 "gp_pickleweed_plant_JESS" 4 10 rand 800 900
** attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr pickup  16 + eat 32 = 48
					attr 194
					bhvr 48
** Will go through some walls
					perm 80
** Physics
					accg 0.1
					aero 1
					fric 25
					elas 90
					tick rand 700 1400
					velo rand -2 2 0
					mvto va05 va06
					slow

** pose ge 5 --> grow to full stage
				elif pose ge 5
					addv va07 9
					pose va07

** pose ge 1 --> grow to second stage
				elif pose ge 1
					addv va07 5
					pose va07
				endi
				addv va01 1
				targ ownr
			repe



** Make the first checking point random.
			setv va01 2
			addv va01 rand 0 1

** If the first spot isn't free, try the next one.
			setv va03 0
			reps 2
				part va01
				doif pose eq 0 and va03 eq 0
					addv va03 rand 1 4
					pose va03
				endi
				addv va01 1
				doif va01 gt 3
					setv va01 2
				endi
			repe
		endi

	endi
endm


*
** Seed timer script
*

*
** Seed script
*


*
** EAT
scrp 2 3 21820 12
	lock
	wait 25
** stim eat plant
	stim writ from 77 1
	wait 25
	kill ownr
	unlk
endm


*
** TIMER
scrp 2 3 21820 9

** Get the room type of the seed
	setv va00 posl
	subv va00 20
	setv va01 posb
	subv va01 70
	setv va02 grap posx posy
	setv va03 rtyp va02

** only survive in drained / boggy soil
	doif carr eq null

** If submerged, check if the part on top of the plant is not submerged
		doif va03 eq 9
			setv va20 posb
			subv va20 70
			setv va21 grap va00 va20
			doif va21 ne -1
				setv va22 rtyp va21
				doif va22 ne 8 and va22 ne 9 and va22 ne -1
** if half in water, set va03 as "boggy soil"
					setv va03 6
				endi
			endi
		endi

		doif va03 ne 6 and va03 ne 7 and va03 ne 1
			kill ownr
			stop
		endi
	endi

** Check if the seed has settled
	doif carr eq null and fall eq 0
		inst
** Grow plant!
		new: comp 2 4 21820 "gp_pickleweed_plant_JESS" 7 15 rand 1000 3000
** attr: invisible 16, bhvr: 0
		attr 16
		bhvr 0
		pat: dull 2 "gp_pickleweed_plant_JESS" 1 11 0 1
		pat: dull 3 "gp_pickleweed_plant_JESS" 1 33 14 1

** Set the tick to 2 to trigger the 'do I have enough space to grow' check script
		tick 2

** test-move it into the space: if it can't grow there, kill it
		doif tmvt va00 va01 eq 0
			kill targ
		else
** If it can grow here, grow!
			mvto va00 va01
		endi
		slow
		targ ownr
		kill ownr
	endi
endm


*
** Removal script 
*

rscr

enum 2 23 21820
	kill targ
next
enum 2 3 21820
	kill targ
next
enum 2 4 21820
	kill targ
next

scrx 2 23 21820 9
scrx 2 23 21820 3
scrx 2 23 21820 1

scrx 2 3 21820 9
scrx 2 3 21820 12
scrx 2 4 21820 9